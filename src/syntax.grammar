@top Program { expression* }

@skip { whitespace | LineComment }

expression {
  logical |
  number |
  null |
  Type |
  Char |
  String |
  Regex |
  Word |
  Color |
  Literal |
  SymbolLiteral |
  Inline |
  Block |
  Dictionary |
  Function
}

logical {
  kwn<"true" | "false" | "maybe", Logical>
}

number {
  Integer | Floating | Version
}

null { kw<"null"> }

Type { ":" identifier }

Literal { "'" identifier }
SymbolLiteral { "'" symbol }

Word { identifier }

Inline { "(" expression* ")" }

Block { "[" expression* "]" }

KeyValuePair { Word ":" expression }

Dictionary { "#[" KeyValuePair* "]" }

Parameter { Word }
Parameters { "[" Parameter* "]" }

Function {
  @specialize<identifier, "function"> Parameters Block
}

@skip {} {
  String {
    '"' (Escape | stringContentDoubleQuote)* '"' |
    "«" stringContentSmartQuote* ("\n" | eof) |
    "{:" stringContentMultiline* ":}" |
    "{" stringContentMultiline* "}" |
    "------\n" stringContentDash* eof 
  }
}

Regex { "{/" regexContent* "/" ("i" | "m" | "s")* "}" }

kw<term> { kwn<term, term> }
kwn<term, name> { @specialize[@name={name}]<identifier, term> }

@tokens {
  whitespace { @whitespace+ }
  digit { @digit }
  letter { @asciiLetter }
  hex { @digit | $[a-fA-F] }
  eof { @eof}

  Integer { digit+ (("e" | "E") ("+" | "-")? digit+)? }
  Floating { digit+ "." digit+ }
  Version { digit+ "." digit+ "." digit+ (letter | digit | "+" | "-" | ".")* }
  @precedence { Version, Floating, Integer }

  Color { "#" (hex hex hex hex hex hex | letter+) }

  Char { "'" (Escape | ![']) "'" }
  Escape { "\\" _ }

  stringContentDoubleQuote { ![\\\n"] }
  stringContentSmartQuote { ![\n] }
  stringContentMultiline { ![}] }
  stringContentDash { _ }

  regexContent { ![\\\n/] }

  @precedence { regexContent, whitespace, LineComment }

  identifierChar { letter | "_" }
  identifier { identifierChar (identifierChar | digit | "?")* }

  symbol { "<=" | "=>" | "<=>" | "<<=" | "=>>" | "<<=>>" | "<-" | "->" | "<->" | "<<-" | "->>" | "<<->>" | "-<" | ">-" | ">-<" | "-<<" | ">>-" | ">>-<<" | "<<" | ">>" | "<<<" | ">>>" | "<--" | "-->" | "<-->" | "<==" | "==>" | "<==>" | "<~" | "~>" | "<~>" | "|>" | "<|" | "<|>" | "=<" | ">=" | "<>" | "<:" | "-:" | ">:" | "~" | "!" | "!!" | "?" | "??" | "@" | "#" | "##" | "###" | "####" | "#####" | "######" | "$" | "%" | "^" | "&" | "*" | "**" | "-" | "--" | "=" | "==" | "=~" | "+" | "++" | "<" | ">" | "/" | "/%" | "//" | "\\" | "\\\\" | "|" | "|-" | "|=" | ".." | "..." | "./" | ":" | "::" | ":=" | "||" | "∅" | "∞" | "∑" | "∏" | "∩" | "∪" | "⊂" | "⊃" | "⊆" | "⊇" | "∈" | "∉" | "∧" | "∨" | "⊻" | "⊼" | "¬" }

  LineComment { ";" ![\n]* }
}

@detectDelim
